\chapter{Principios del composición}

La sensibilidad es un conjunto de sentimientos que pululan en nuestro subconsciencia armonizadas por nuestro temperamento que acoge nuestro existencia es por eso que se el arte expresa esta armonía con la yuxtaposición adecuada de elementos gráficos es decir es el ritmo que genera un bello lenguaje visual dominarlo es cuestión de nuestro instinto creador conceptual de la realidad anexada a cada ser humano
\subsection{El ritmo} Permutación de un conjunto de series (aritméticas geométricas cualquier otra que puedas inventar) convergentes o no (divergente, constantes) donde una serie puede depender de otra u otras, estas series pueden esa relacionadas con cantidades numéricas tales como ancho, largo, profundidad, distancias entre los objetos, la dirección de los ejes de los objetos con respecto un punto o otra serie, cantidad de rugosidad etcétera.


\begin{figure}
	\begin{asy}
	import three;
import math;
import graph3;
size(300,0);
path3 g =(0,1.1,0) -- (0,1.1,0.1) .. tension 1.1 ..	(0,2,0.8) ..	(0,1,3) ..tension 1 and 5 .. (0,0.8,4.5) ..tension 50 and 50 .. (0,0.5,5) -- (0,0.5,7)-- (0,0.8,9)-- (0,0.8,10);
surface lampshade=surface(g, c=O, n=100, axis=Z);
draw(lampshade, white+opacity(0.5));
dot(g, black);
draw(rotate(98,Y)*shift((-1.5,3,0))*scale3(.5)*lampshade, orange+opacity(0.5));
axes3("$x$","$y$","$z$", Arrow3);
draw(surface((-2,-2,0)--(2,-2,0)--(2,2,0)--(-2,2,0)--cycle), white+opacity(0.5));
\end{asy}
\caption{Revolucion}
\end{figure}




 \begin{asy}
 size(7.5cm,0);
 import solids;
 import graph3;

 currentprojection=orthographic(40,10,10);

 real a=2, r=4, h=5, l=(r/h)^2, m=sqrt(l*h^2-a^2);
 limits((-r,-r,-h),(2r,r,1.2*h));
 triple pA1=(0,0,-h), pA2=-pA1, pB1=(r,0,-h), pB2=-pB1,
 pO=(0,0,0), pI=(a,0,-sqrt(a^2/l));
 triple F1(real y){return (a,y,sqrt((a^2+y^2)/l));}
 triple F2(real y){return (a,y,-sqrt((a^2+y^2)/l));}
 path3 b1=graph(F1,-r,r,operator ..),
 b2=graph(F2,-r,r,operator ..);
 triple v1=(0,2m,0),v2=(0,0,2h),p0=(a,-m,-h);
 path3 pl1=plane(v1,v2,p0);

 draw(cone(pA1,r,h,Z,1),1bp+blue,longitudinalpen=nullpen);
 draw(cone(pA2,r,h,-Z,1),1bp+blue,longitudinalpen=nullpen);

 // Figure prévue initialement avec transparence du cône (+opacity(.6))
 // supprimée dans les 2 lignes suivantes pour un meilleur rendu ci-contre.
 draw(surface(cone(pA1,r,h,Z,1)),lightgray+white);
 draw(surface(cone(pA2,r,h,-Z,1)),lightgray);

 draw(surface(pl1),green+opacity(.6));
 draw(pI--pB1^^pA2--pB2);
 draw(pB1--pA1--pA2^^pI--pO--pB2,dashed);
 draw(b1^^b2,1bp+red);

 dot(pB1--pA1--pA2^^pI--pO--pB2);
 dot((a,m,-h)--(a,m,h)--(a,-m,h)--(a,-m,-h));
 dot(Label("$a$",align=-Y+X),(a,0,0));
 label("$O$",pO,2E);
 xaxis3("$x$",Arrow3);
 yaxis3("$y$",Arrow3);
 zaxis3("$z$",Arrow3);
  \end{asy}


\begin{asy}
import solids;
currentprojection=orthographic(8.5,9.5,8);
currentlight=(0,5,5);

size(8cm,0);

real R=3, a=1, d=R+2a;

// On définit un tore et on le trace.
revolution tore=revolution(shift(R*X)*Circle(O,a,Y,32),Z);
surface s=surface(tore);
draw(s,white);

// On définit une courbe et on la trace...
path3 g=d*unit(X+.3Y)..0.5(X-Y)..d*unit(-X-3Y)
..(-d,0,a)..0.5(Y-X)..d*unit(.5X+Y);
draw(g,2bp+blue);
// ... en indiquant en vert les 6 points la définissant.
dot(g,4bp+green);

// On définit les points d'intersection du tore et de la courbe...
triple[] points=intersectionpoints(g,s);
// ... que l'on place en rouge.
dot(points,6bp+red);

\end{asy}


\begin{asy}
import graph3;
size(7cm, 0);
currentprojection = orthographic(3, 4, 5);
real epsilon = .0001;
// Returns the derivative f'(t) of a parametrized function f: R -> R^3.
triple derivative(triple f(real), real t, real dx=epsilon)
{
    return (f(t + dx) - f(t - dx)) / 2dx;
}
// Returns a vector starting from `start` with a direction `direction`.
path3 pos(triple start, triple direction)
{
    return shift(start) * (O -- direction);
}
// Base vector
triple base(real t){
    return (3cos(t / sqrt(10)), 3sin(t / sqrt(10)), t / sqrt(10));
}
// Tangent vector
triple tangent(real t){
    return unit(derivative(base, t));
}
// Normal vector
triple normal(real t){
    return unit(derivative(tangent, t));
}
// Binormal vector
triple binormal(real t){
    return cross(tangent(t), normal(t));
}
// u-frame vector
triple uFrame(real t, real u){
    return cos(u) * normal(t) + sin(u) * binormal(t);
}
// u-circle center
triple uCenter(real t, real u){
    return base(t) + uFrame(t, u);
}
// u-circle
path3 uCircle(real t, real u, real radius=1){
    triple n = cross(tangent(t), uFrame(t, u));
    return circle(uCenter(t, u), radius, normal=n);
}
// Parametrized cycloidal surface
triple paramCycloid(real t, real u){
    assert(0 <= u && u <= 2pi, "u should be in range [0, 2pi]");
    return uCenter(t, u) - sin(t) * tangent(t) - cos(t) * uFrame(t, u);
}
triple paramCycloid(pair z){
    return paramCycloid(xpart(z), ypart(z));
}
triple principalCycloid(real t){
    return paramCycloid(t, 0);
}
// t range
real tMin = 0;
real tMax = pi * sqrt(10);
// Draw curves
path3 baseCurve = graph(base, tMin, tMax, operator ..);
draw(baseCurve, black);

 path3 centerCurve = graph(
     new triple (real t) { return uCenter(t, 0); },
     tMin,
     tMax,
     operator ..
 );
 draw(centerCurve, orange);

path3 principalCycloidCurve = graph(principalCycloid, tMin, tMax, operator ..);
draw(principalCycloidCurve, yellow);

// Draw TNB frames
int steps = 5;
for (int i = 0; i < steps; ++i) {
    real t = tMin + (tMax - tMin) / steps * i;
    // Current point on the base curve
    triple curr = base(t);
    dot(curr);
    draw(pos(curr, tangent(t)), red + linewidth(.4pt), arrow=Arrow3());
    draw(pos(curr, normal(t)), green + linewidth(.4pt), arrow=Arrow3());
    draw(pos(curr, binormal(t)), blue + linewidth(.4pt), arrow=Arrow3());
    draw(uCircle(t, 0), red + linewidth(.4pt));
}
// Draw the cycloidal surface
var cycloidalSurface = surface(paramCycloid, (tMin, 0), (tMax, 2pi), Spline);
var surfacepen = material(
    blue+opacity(.3),
    emissivepen=gray(.2),
    shininess=.5
);
draw(cycloidalSurface, surfacepen=surfacepen);
\end{asy}

\begin{description}
  \item[a] Considerando  la progresión geometría o aritmética a cualquier otra sucesión de las distancias entre los centros de gravedad de dos objetos adyacentes de mod que guarden alguna sucesión creciente o decreciente de las longitudes entre los elementos.

  \item[b] la metamorfosis de su estructura que estos sufren al converger al foco visual o al divergir de ella.

  \item[c] el color, la textura, el tamaño, etc.
\end{description}

  Tenemos un ejemplo
$AB,$ $CD,$ $EF$ y $GH$ son los objetos a los cuales se le aplico una serie en la distancia horizontal de sus longitudes tal com


En la siguiente figura se observa cuatro series, la primera es aquella generada por $\alpha$ (serie aritmética $\cdots, 2\theta, cdots$) la segunda por (la serie) geométrica) $\theta$ la tercera generada por la líneas de grosor variable donde también esta serie de compone de otras series tales como la distancia entre los puntos $P'',$ $P',$ $P$ son guiados por la segunda serie anterior es decir horizontalmente coinciden con las líneas  verticales que parten de los extremos $C,$ $D,$ y $F$ respectivamente, la distribución vertical de estos puntos  obedece a otra serie finalmente la ultima

\begin{figure}
\begin{center}

\end{center}
\caption{Series en una Composición}\label{Ogw}
\end{figure}

En el siguiente ejemplo  se ganara el ritmo partir de dos series de circunferencia  concéntricas, en el primer grupo  se ubican los puntos $A, B, C$ luego se obtiene los puntos $P,$ $Q,$ y $R$ al rotar $90^{\circ}$ el punto $O$ centrado en $A,$ $B,$ y $C$ respectivamente; a partir de estos puntos se trazan tangentes sobre la segunda serie de circunferencias, ubicandose los punto $I''$ y $I'$ sobre la circunferencia menor en la serie, con la

\begin{figure}
\begin{center}
\begin{asy}
import geometry; size(8cm);

inversion inv=inversion(1,(-4.5,0));
path g1=inv*line((-1,0),(-1,1)),
g2=inv*line((1,0),(1,1));
fill(g1,lightgrey); draw(g1,linewidth(bp));
unfill(g2); draw(g2,linewidth(bp));

int n=40;
for (int i=-n; i <= n; ++i) {
path g=inv*circle((point) (0,2*i),1);
fill(g,(1-abs(i)/n)*yellow);
draw(g,bp+red); draw(g,blue);
}
shipout(bbox(5mm,Fill(rgb(0.95,0.95,0.8))));

\end{asy}
\end{center}
\caption{series en una composición}\label{Og}
\end{figure}




\section{Wwwwww}

\begin{figure}
	\begin{asy}
size(300,0);
import graph3;
triple F(pair uv) {
real t = uv.x;
real r = uv.y;
return (cos(t) + r*cos(t)*sin(t/2),
sin(t) + r*sin(t)*sin(t/2),
r*cos(t/2));
}
real r = 0.3;
surface moeb = surface(F, (0,-r), (2pi,r), Spline);
draw(moeb, surfacepen=material(blue, emissivepen=0.15 white), meshpen=black+thick());
	\end{asy}
\caption{wwwwwww}
\end{figure}
